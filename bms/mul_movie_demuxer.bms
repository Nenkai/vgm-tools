# MUL movie demuxer by bnnm
#
# for movies in various Tomb Raider games (use gibbed tools to extract .mul first)

######################

get SRATE long
endian guess SRATE

get FILE_SIZE asize
set OFFSET long 0

math IS_VIDEO = 0
math IS_AUDIO = 0

append #on
do 
    goto OFFSET

    if OFFSET == 0
        math BLOCK_SIZE = 0x800
        math IS_VALID = 1
    else
        get BLOCK_TYPE long
        get BLOCK_SIZE long
        
        math BLOCK_SIZE += 0x10
        if BLOCK_TYPE == 0x00000000
            math IS_VALID = 1
            math IS_AUDIO = 1
        else    
            math IS_VALID = 0
            math IS_VIDEO = 1
        endif
    endif


    if IS_VALID == 1
        log MEMORY_FILE OFFSET BLOCK_SIZE
    endif

    math OFFSET += BLOCK_SIZE


while OFFSET < FILE_SIZE
append #off


if IS_VIDEO == 0
  print "File doesn't contain video"
  cleanexit
endif

if IS_AUDIO == 0
  print "File doesn't contain audio"
  cleanexit
endif

get BASENAME basename
get EXT extension
string NAME p= "%s.audio.%s" BASENAME EXT
get SIZE asize MEMORY_FILE

log NAME 0 SIZE MEMORY_FILE
